%; replgroupcomps
%;
%; form (components) file for `repl -group'
%;
%; Check the following headers to create reply addresses.
%;
%; To: Mail-Followup-To
%;
%; OR
%;
%; To: Mail-Reply-To   (or)
%;     Reply-To        (or)
%;     From            (or)
%;     Sender          (or)
%;     Return-Path
%;
%; AND
%;
%; cc: To              (and)
%;     cc              (and)
%;     personal address
%;
%(lit)%(formataddr{mail-followup-to})\
%<(nonnull)%(void(width))%(putaddr To: )\n\
%|\
%(lit)%(formataddr %<{mail-reply-to}%?{reply-to}%?{from}%?{sender}%?{return-path}%>)\
%<(nonnull)%(void(width))%(putaddr To: )\n%>\
%(lit)%(formataddr{to})%(formataddr{cc})%(formataddr(me))\
%<(nonnull)%(void(width))%(putaddr cc: )\n%>%>\
%;
Fcc: %<{fcc}%{fcc}%|+outbox%>
Subject: %<{subject}Re: %{subject}%>
%;
%; Make References: and In-reply-to: fields for threading.
%; Use (void), (trim) and (putstr) to eat trailing whitespace.
%;
%<{message-id}In-reply-to: %{message-id}\n%>\
%<{message-id}References: \
%<{references}%(void{references})%(trim)%(putstr) %>\
%(void{message-id})%(trim)%(putstr)\n%>\
Comments: In-reply-to \
%<{from}%(void{from})%?(void{apparently-from})%|%(void{sender})%>\
%(trim)%(putstr)\n\
   message dated "%<(nodate{date})%{date}%|%(tws{date})%>."
--------
